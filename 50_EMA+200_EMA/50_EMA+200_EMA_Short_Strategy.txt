// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© krosenfeld7

//@version=4
strategy("50 EMA + 200 EMA Short Strategy (Not Good)", overlay = true, pyramiding = 0, 
         margin_long = 100, margin_short = 100, default_qty_type = strategy.percent_of_equity, 
         default_qty_value = 20, initial_capital = 2000, process_orders_on_close = true)

// Set the starting point for backtesting
start = input(title="Backtesting Start", type=input.time, defval=timestamp("1 Jan 2015"))
// Expiration time on orders
maxTimeOpen = input(title="Order Expiration", type=input.integer, defval=100, minval=1)
useAtr = input(title="Use ATR?", type=input.bool, defval=false)

// Algorithm / Strategy Goes here
// Set long / short variables to execute positions below

// Creating 50 period and 200 period EMAs
len1 = input(50, minval=1, title="Short EMA Length")
src1 = input(close, title="Short EMA Source")
ema50 = ema(src1, len1)
plot(ema50, title="Short EMA", color=color.blue, linewidth=3)
len2 = input(200, minval=1, title="Long EMA Length")
src2 = input(close, title="Long EMA Source")
ema200 = ema(src2, len2)
plot(ema200, title="Long EMA", color=color.white, linewidth=3)

// Number of preceding candles to use in algorithm
lookback = input(title="Lookback", type=input.integer, defval=3, minval=1)

longCondition = (barssince(crossover(close, ema50)) <= lookback) and (close > ema50 and close > open) and ema50 > ema200
shortCondition = (barssince(crossunder(close, ema50)) <= lookback) and (close < ema50 and close < open) and ema50 < ema200

search_preceding_candles(_cond, _length) =>
    _found = false
    for i = 1 to _length
        if _cond[i]
            _found := true
            break
    _found

long = (longCondition and not search_preceding_candles(longCondition, lookback)) ? 1 : 0
short = (shortCondition and not search_preceding_candles(longCondition, lookback)) ? 1 : 0

// Average True Range
length = input(title="ATR Length", defval=14, minval=1)
atrMultiplier = input(title="ATR Multiplier", type=input.float, defval=1, minval=0.1)
atr = atr(length)

shortProfitPercent = input(title = "Profit %",
     type = input.float, minval = 0.0, step = 0.1, defval = 10) / 100
shortLossPercent = input(title = "Loss %",
     type = input.float, minval = 0.0, step = 0.1, defval = 10) / 100

// Calculating exact prices for stop loss and exit prices
shortExitPercentPrice = strategy.position_avg_price * (1 - shortProfitPercent)
shortStopPercentPrice = strategy.position_avg_price * (1 + shortLossPercent)

// Profit & Loss via ATR
shortExitPrice = useAtr ? strategy.position_avg_price - atr * atrMultiplier : shortExitPercentPrice
shortStopPrice = useAtr ? strategy.position_avg_price + atr * atrMultiplier : shortStopPercentPrice

// Plotting profit / loss targets
plot(series = (strategy.position_size < 0) ? shortExitPrice : na, color = #33b802, style = plot.style_linebr, transp = 0, linewidth = 3, title = "Short Target")
plot(series = (strategy.position_size < 0) ? shortStopPrice : na, color = #ff0900, style = plot.style_linebr, transp = 0, linewidth = 3, title = "Short Stop Loss")

// Submit entry orders
if time >= start
    strategy.entry(id = "Short", long = strategy.short, comment = "Short Open", when = short)

// Submit exit orders
strategy.exit(id = "Short", limit = shortExitPrice, stop = shortStopPrice, comment = "Short Close", when = strategy.position_size < 0)
strategy.close(id = "Short", comment = "Expired: >" + tostring(maxTimeOpen), when = (barssince(change(strategy.opentrades)) + 1) >= maxTimeOpen)
